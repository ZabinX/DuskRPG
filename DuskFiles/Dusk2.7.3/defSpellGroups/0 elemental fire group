#Fire Shield
#
# Halves the damage done to the caster by spells
# from the elemental ice group
#
# Doubles the damage done to the caster by spells
# from the elemental fire group
#
# If caster already has ice shield in place,
# both spells cancel each other out
#
#
if ! hasitem caster "Fire Dragon Scale"
    chat caster "You cannot cast this spell without a Fire Dragon Scale."
    endscript
end
removeitem caster "Fire Dragon Scale"
number duration * caster inte 6
if variableisstring param0
    livingthing target local caster param0
    if and ! isplayer target and ! ispet target ! ismob target
        chat caster "You do not see them here."
        endscript
    end
    chat caster concat concat "You summon protective fire to guard " name target "."
    chat target concat name caster " surrounds you with protective fire."
    if hascondition target "ice shield"
        chat target "Fire and ice collide with explosive force."
        chat target "You are remarkably unharmed."
        removecondition target "ice shield"
        endscript
    end
    addconditionwithduration target "fire shield" duration
    endscript
end
chat caster "You surround yourself with protective fire."
if hascondition caster "ice shield"
    chat caster "Fire and ice collide with explosive force."
    chat caster "You are remarkably unharmed."
    removecondition caster "ice shield"
    endscript
end
addconditionwithduration caster "fire shield" duration
endscript
